#use-added-syntax(esir)
defpackage ocdb/checks:
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness
  import ocdb/space-derating
  import ocdb/generator-utils

public pcb-check io () :
  for n in nets(self) do :
    ; Check compatibility between two single-ended logic pins (bi directional checks)
    if (length(refs(n))  == 2) and all?(has-property?{_.digital-io}, refs(n)) : 
      val p = refs(n)
      if property(p[0].type) == "io" and 
         property(p[1].type) == "io" :
        #CHECK(property(p[0].vOH) >= property(p[1].vIH))
        #CHECK(property(p[0].vOH) <= property(p[1].rated-voltage)[1])
        #CHECK(property(p[0].vOL) <= property(p[1].vIL))
        #CHECK(property(p[0].vOL) >= property(p[1].rated-voltage)[0])
        #CHECK(property(p[1].vOH) >= property(p[0].vIH))
        #CHECK(property(p[1].vOH) <= property(p[0].rated-voltage)[1])
        #CHECK(property(p[1].vOL) <= property(p[0].vIL))
        #CHECK(property(p[1].vOL) >= property(p[0].rated-voltage)[0])
      else if property(p[0].type) == "i" and 
              property(p[1].type) == "o" :
        #CHECK(property(p[1].vOH) >= property(p[0].vIH))
        #CHECK(property(p[1].vOH) <= property(p[0].rated-voltage)[1])
        #CHECK(property(p[1].vOL) <= property(p[0].vIL))
        #CHECK(property(p[1].vOL) >= property(p[0].rated-voltage)[0])
      else if property(p[0].type) == "o" and 
              property(p[1].type) == "i" :
        #CHECK(property(p[0].vOH) >= property(p[1].vIH))
        #CHECK(property(p[0].vOH) <= property(p[1].rated-voltage)[1])
        #CHECK(property(p[0].vOL) <= property(p[1].vIL))
        #CHECK(property(p[0].vOL) >= property(p[1].rated-voltage)[0])
      else :
        println("Invalid logic connection between: %_ and %_" % [property(p[0].type) property(p[1].type)])
        #CHECK(false)
    ; Check compatibility between lvds pairs (bi directional checks)
    if (length(refs(n))  == 2) and all?(has-property?{_.lvds}, refs(n)) : 
      val p = refs(n)
      #CHECK(property(p[0].vo-cm)[2]   <= property(p[1].vi-cm)[2])
      #CHECK(property(p[0].vo-diff)[0] >= property(p[1].vi-diff)[0])
      #CHECK(property(p[1].vo-cm)[2]   <= property(p[0].vi-cm)[2])
      #CHECK(property(p[1].vo-diff)[0] >= property(p[0].vi-diff)[0])

; Checks a power pin located on a component
public pcb-check power-pin (p:Ref) :
  #CHECK(has-property?(p.power-pin))
  #CHECK(has-property?(p.voltage))
  if has-property?(p.voltage):
    check-rated-voltage(p)

defn check-rated-voltage (p:Ref) :
  val rating = property(p.rated-voltage)
  val v = property(p.voltage)
  match(rating, v):
    (r:Double, v:Double) : #CHECK(property(p.rated-voltage) >= v)
    (r:[Double,Double], v:Double) : 
      #CHECK(property(p.rated-voltage)[1] >= v)
      #CHECK(property(p.rated-voltage)[0] <= v)
    (r:[Double,Double], v:[Double|Double|Double]) : 
      #CHECK(property(p.rated-voltage)[1] >= v[2])
      #CHECK(property(p.rated-voltage)[0] <= v[0])
    (r, v) : false

; Checks if voltage on a pin is within acceptable limits
public pcb-check voltage-levels (p:Ref, range:[Double,Double,Double]) :
  #CHECK(has-property?(p.voltage))
  #CHECK(property(p.voltage)[0] >= range[0])
  #CHECK(property(p.voltage)[2] <= range[2])

; Checks resistors agains operating point and environment
public pcb-check resistor-check (r:Ref) :
  #CHECK(has-property?(r.resistor))
  #CHECK(has-property?(r.operating-point))
  val [vpk i temp] = [lookup(property(r.operating-point), `voltage-pk) lookup(property(r.operating-point), `current-rms) property(r.temperature)]
  val power = property(r.resistance) * pow(i, 2.0)
  #CHECK(abs(vpk[0]) <= property(r.rated-voltage))
  #CHECK(abs(vpk[1]) <= property(r.rated-voltage))
  if has-property?(r.derating) :
    ; Derate the power based on the piecewise linear curve from MFG
    val derating = PWL(property(r.derating))[temp]
    val pwr = derating * property(r.rated-power)
    #CHECK(pwr * DERATE-RESISTOR-POWER > power)
  else :
    #CHECK(property(r.rated-power) * DERATE-RESISTOR-POWER > power)

public pcb-check capacitor-check (c:Ref) :
  #CHECK(has-property?(c.capacitor))
  #CHECK(has-property?(c.operating-point))
  val [vpk temp] = [lookup(property(c.operating-point), `voltage-pk) property(c.temperature)]
  switch(property(c.type)) :

    "ceramic" :
      val vpk-derate-wc = PWL(DERATE-CAPACITOR-MLCC-VPK-WC)[temp]
      #CHECK(vpk[0] <= (vpk-derate-wc * property(c.rated-voltage)))
      #CHECK(vpk[1] <= (vpk-derate-wc * property(c.rated-voltage)))
      val vpk-derate-nom = PWL(DERATE-CAPACITOR-MLCC-VPK-NOM)[temp]
      #CHECK(vpk[0] <= (vpk-derate-nom * property(c.rated-voltage)))
      #CHECK(vpk[1] <= (vpk-derate-nom * property(c.rated-voltage)))
      #CHECK(temp <= min(DERATE-CAPACITOR-MLCC-TEMP, property(c.rated-temperature)[1]))

    "electrolytic" :
      #CHECK(has-property?(c.anode))
      #CHECK(has-property?(c.electrolyte))
      val [i-pk i-rms] = [lookup(property(c.operating-point), `current-pk) lookup(property(c.operating-point), `current-rms)]

      if (property(c.anode) == "tantalum" and property(c.electrolyte) == "polymer") :
        #CHECK(vpk[1]   <= (DERATE-CAPACITOR-ETANTPOLY-VPK * property(c.rated-voltage)))
        #CHECK(temp  <= min(DERATE-CAPACITOR-ETANTPOLY-TEMP, property(c.rated-temperature)[1]))
        #CHECK(i-pk  <= property(c.rated-current-pk))
        #CHECK(i-rms <= PWL(property(c.rated-current-rms))[temp])
        #CHECK(vpk[0]  >= 0.0)

      else if (property(c.anode) == "tantalum" and property(c.electrolyte) == "manganese-dioxide") :
        #CHECK(i-pk <= property(c.rated-current-pk))
        #CHECK(i-rms <= PWL(property(c.rated-current-rms))[temp])
        #CHECK(vpk[0]  >= 0.0)
        if i-pk <= 2.0 :
          val vpk-derate-wc = PWL(DERATE-CAPACITOR-ETANTMNO2-VPK-WC)[temp]
          val vpk-derate-nom = PWL(DERATE-CAPACITOR-ETANTMNO2-VPK-NOM)[temp]
          #CHECK(vpk[1]  <= (vpk-derate-wc * property(c.rated-voltage)))
          #CHECK(vpk[1]  <= (vpk-derate-nom * property(c.rated-voltage)))
          #CHECK(temp <= min(DERATE-CAPACITOR-ETANTMNO2-TEMP-NOM, property(c.rated-temperature)[1]))
          #CHECK(temp <= min(DERATE-CAPACITOR-ETANTMNO2-TEMP-WC, property(c.rated-temperature)[1]))
        else if i-pk > 2.0 :
          #CHECK(vpk[1]  <= (DERATE-CAPACITOR-ETANTMNO2-VPK * property(c.rated-voltage)))
          #CHECK(temp <= min(DERATE-CAPACITOR-ETANTMNO2-TEMP-NOM, property(c.rated-temperature)[1]))

      else :
        println("Unhandled electrolytic type for capacitor check on %_" % [c])

    "mica" :
      #CHECK(vpk[0] <= (DERATE-CAPACITOR-MICA-VPK * property(c.rated-voltage)))
      #CHECK(vpk[1] <= (DERATE-CAPACITOR-MICA-VPK * property(c.rated-voltage)))
      #CHECK(temp <= min(DERATE-CAPACITOR-MICA-TEMP, property(c.rated-temperature)[1]))

    else : println("Unhandled type for capacitor check on %_" % [c])

  #CHECK(temp >= property(c.rated-temperature)[0])