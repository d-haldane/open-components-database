#use-added-syntax(esir)
defpackage ocdb/texas-instruments/TLV743P:
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness
  import ocdb/pinspec
  import ocdb/land-patterns
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/bundles
;[strip]
  import ocdb/symbols
  import ocdb/box-symbol
;[/strip]

public pcb-component component (v-out:Double) :
  pin en
  pin gnd
  pin in
  pin out
  pin nc
  name = "TLV743P"
  manufacturer = "Texas Instruments"
  description = "TLV743P 300-mA, Low-Dropout Regulator"
  val [code spice-code] = switch(v-out) :
    1.5 : ["15" "TLV74315P"]
    2.5 : ["25" "TLV74325P"]
    3.3 : ["33" "TLV74333P"]
  mpn = to-string("TLV743%_P" % [code])
  val ps = PinSpec $ #TABLE :
    [Ref     | Int ...   | Dir]
    [gnd | 1 | Down]
    [en  | 2 | Right]
    [out | 3 | Up]
    [in  | 4 | Up]
    [nc  | 5 | Up]
;[strip]
  make-box-symbol(ps)
;[/strip]
  assign-package(SOT95P280X145-5N, ps)

  properties(in):
    power-pin => true
    rated-voltage => [1.4 5.5]
  properties(out):
    supply-pin => true
    voltage => v-out

  spice:
    "[X] {en} {gnd} {in} {out} {nc} {spice-code}"

public pcb-module module (v-out:Double) :
  pin vin
  pin vout
  pin gnd
  pin en
  inst ps : {ocdb/texas-instruments/TLV743P/component(v-out)}
  cap-strap(gnd, ps.in, 1.0)
  cap-strap(gnd, ps.out, 1.0)
  net (vin, ps.in)
  net (vout, ps.out)
  net (gnd, ps.gnd)
  net (en, ps.en)
